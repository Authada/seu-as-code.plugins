dependencies {
    compile 'net.java.dev.jna:jna:4.1.0'
    compile 'net.java.dev.jna:jna-platform:4.1.0'
    compile 'commons-codec:commons-codec:1.10'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    name project.name
    description project.description
    url project.websiteUrl
    inceptionYear '2015'

    scm {
        connection "scm:git:${project.scmUrl}"
        developerConnection "scm:git:${project.scmUrl}"
        url project.websiteUrl
    }

    issueManagement {
        system 'GitHub'
        url project.issueTrackerUrl
    }

    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    organisation {
        name 'QAware GmbH'
        url 'https://www.qaware.de'
    }

    developers {
        developer {
            id 'lreimer'
            name 'Mario-Leander Reimer'
            email 'mario-leander.reimer@qaware.de'
            organization 'QAware GmbH'
            roles { role 'Developer' }
        }
        developer {
            id phxql
            name 'Moritz Kammerer'
            email 'moritz.kammerer@qaware.de'
            organization 'QAware GmbH'
            roles { role 'Developer' }
        }
    }
}

publishing {
    publications {
        credentialsPlugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = 'unknown'
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = 'unknown'

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['credentialsPlugin']
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'credentials', 'security', 'seu-as-code']
        websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
            attributes = [
                    'gradle-plugin': ['de.qaware.seu.as.code.credentials', project.group, project.name].join(':')
            ]
        }
    }
}

pluginBundle {
    website = project.websiteUrl
    vcsUrl = project.scmUrl

    plugins {
        credentialsPlugin {
            id = 'de.qaware.seu.as.code.credentials'
            displayName = 'Gradle Credentials plugin'
            description = project.description
            tags = ['credentials', 'security', 'seu-as-code']
            version = project.version
        }
    }
}